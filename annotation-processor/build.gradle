plugins {
    id "com.github.johnrengelman.shadow" version "7.1.2"
    id 'java-library'
    id 'com.diffplug.spotless'
}

repositories {
    mavenCentral()
    maven { url uri("https://maven.fabricmc.net") }
    maven { url "https://maven.neoforged.net/releases" }
}

dependencies {
    implementation 'com.google.code.gson:gson:2.10.1'
    annotationProcessor 'com.google.auto.service:auto-service:1.1.1'
    compileOnly 'com.google.auto.service:auto-service:1.1.1'

    shadow "net.fabricmc:sponge-mixin:0.12.5+"
    implementation "net.fabricmc:sponge-mixin:0.12.5+"
    // Platform classes
    runtimeOnly "net.fabricmc:fabric-loader:${rootProject.fabric_loader_version}"
    implementation project(":annotations")
    implementation project(":annotation-processor-hack")
}

tasks.withType(JavaCompile) {
    options.compilerArgs += '--enable-preview'
}

shadowJar {
    dependencies {
        include(dependency('net.fabricmc:sponge-mixin:.*'))
    }
    exclude 'META-INF/services/org.spongepowered*'
    exclude 'META-INF/services/cpw*'
    relocate ('org.spongepowered', 'shadow.org.fury_phoenix.shadow.spongepowered')
    minimize()
}

spotless {
    java {
        removeUnusedImports()
    }
}
version = '1.1.1'
