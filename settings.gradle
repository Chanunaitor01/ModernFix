pluginManagement {
    repositories {
        gradlePluginPortal()
        maven { url "https://maven.fabricmc.net/" }
        maven { url "https://maven.architectury.dev/" }
        maven { url "https://maven.minecraftforge.net/" }
    }
    resolutionStrategy {
            eachPlugin {
                if (requested.id.id == "com.github.johnrengelman.shadow") {
                    useModule("gradle.plugin.com.github.johnrengelman:shadow:${shadow_version}")
                }
            }
    }
}

include("annotation-processor")
include("annotations")
include("test_agent")
include("common")

def current_platforms = getProperty("enabled_platforms").tokenize(',')
current_platforms.each { it ->
    def platform_name = it.trim()
    include(platform_name)
    def testmodFolder = new File(platform_name + "/" + "testmod")
    if(testmodFolder.isDirectory()) {
        include(platform_name + ":testmod")
    }
}

rootProject.name = 'modernfix'
